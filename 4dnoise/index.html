<!DOCTYPE html>
<html lang="en">
<head>
<style>
</style>
</head>
<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r71/three.min.js"></script>
<script src="../lib/Trackball.js"></script>
<script id="noise" type="x-shader/x-fragment">
  //
  // Description : Array and textureless GLSL 2D/3D/4D simplex 
  //               noise functions.
  //      Author : Ian McEwan, Ashima Arts.
  //  Maintainer : ijm
  //     Lastmod : 20110822 (ijm)
  //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
  //               Distributed under the MIT License. See LICENSE file.
  //               https://github.com/ashima/webgl-noise
  // 

  vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

  float mod289(float x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

  vec4 permute(vec4 x) {
       return mod289(((x*34.0)+1.0)*x);
  }

  float permute(float x) {
       return mod289(((x*34.0)+1.0)*x);
  }

  vec4 taylorInvSqrt(vec4 r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }

  float taylorInvSqrt(float r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }

  vec4 grad4(float j, vec4 ip)
    {
    const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
    vec4 p,s;

    p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
    p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
    s = vec4(lessThan(p, vec4(0.0)));
    p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www; 

    return p;
    }
              
  // (sqrt(5) - 1)/4 = F4, used once below
  #define F4 0.309016994374947451

  float snoise(vec4 v)
    {
    const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
                          0.276393202250021,  // 2 * G4
                          0.414589803375032,  // 3 * G4
                         -0.447213595499958); // -1 + 4 * G4

    // First corner
      vec4 i  = floor(v + dot(v, vec4(F4)) );
      vec4 x0 = v -   i + dot(i, C.xxxx);

    // Other corners

    // Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
      vec4 i0;
      vec3 isX = step( x0.yzw, x0.xxx );
      vec3 isYZ = step( x0.zww, x0.yyz );
    //  i0.x = dot( isX, vec3( 1.0 ) );
      i0.x = isX.x + isX.y + isX.z;
      i0.yzw = 1.0 - isX;
    //  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
      i0.y += isYZ.x + isYZ.y;
      i0.zw += 1.0 - isYZ.xy;
      i0.z += isYZ.z;
      i0.w += 1.0 - isYZ.z;

      // i0 now contains the unique values 0,1,2,3 in each channel
      vec4 i3 = clamp( i0, 0.0, 1.0 );
      vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
      vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

      //  x0 = x0 - 0.0 + 0.0 * C.xxxx
      //  x1 = x0 - i1  + 1.0 * C.xxxx
      //  x2 = x0 - i2  + 2.0 * C.xxxx
      //  x3 = x0 - i3  + 3.0 * C.xxxx
      //  x4 = x0 - 1.0 + 4.0 * C.xxxx
      vec4 x1 = x0 - i1 + C.xxxx;
      vec4 x2 = x0 - i2 + C.yyyy;
      vec4 x3 = x0 - i3 + C.zzzz;
      vec4 x4 = x0 + C.wwww;

    // Permutations
      i = mod289(i); 
      float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
      vec4 j1 = permute( permute( permute( permute (
                 i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
               + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
               + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
               + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

    // Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
    // 7*7*6 = 294, which is close to the ring size 17*17 = 289.
      vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

      vec4 p0 = grad4(j0,   ip);
      vec4 p1 = grad4(j1.x, ip);
      vec4 p2 = grad4(j1.y, ip);
      vec4 p3 = grad4(j1.z, ip);
      vec4 p4 = grad4(j1.w, ip);

    // Normalise gradients
      vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
      p0 *= norm.x;
      p1 *= norm.y;
      p2 *= norm.z;
      p3 *= norm.w;
      p4 *= taylorInvSqrt(dot(p4,p4));

    // Mix contributions from the five corners
      vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
      vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
      m0 = m0 * m0;
      m1 = m1 * m1;
      return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
                   + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

    }


  float harmonicSeries( vec3 coord, float time ) {

    float n = 0.0;

    n += 0.7    * snoise(vec4(coord , time));
    n += 0.25   * snoise(vec4(coord * 2.0 , time));
    n += 0.125  * snoise(vec4(coord * 4.0 , time));
    n += 0.0625 * snoise(vec4(coord * 8.0 , time));

    return n;

  }
</script>
<script id="fragmentShaderPlane" type="x-shader/x-fragment">
  uniform float time;
  varying vec2 vUv;
  uniform float time4D;
  uniform float timeTranslation;

  void main(void){
    vec3 coord = vec3(vUv.x + timeTranslation,vUv.y,1.0);
    float n = harmonicSeries( coord, time4D );
    gl_FragColor = vec4(vec3(0.5, 0.5, 0.5) + n,1.0);
  }
</script>
<script id="vertexShaderPlane" type="x-shader/x-vertex">
  varying vec2 vUv;
  uniform vec3 scale;
  uniform float time;

  void main( void ) {
    vUv = uv;
    gl_Position = projectionMatrix * modelViewMatrix * vec4(
      position, 1.0);
  }
</script>
<script id="fragmentShaderNoise" type="x-shader/x-fragment">
      uniform float time;
      // The letters "U" and "V" denote the axes of the 2D texture
      // varying vec2 vUv;
      varying vec3 vPosition;
      uniform float time4D;
      uniform float timeTranslation;

      void main( void ) {

        // vec3 coord = vec3(vUv,1.0);
        vec3 coord = vec3(vPosition.x + timeTranslation,vPosition.y,vPosition.z);
        float n = harmonicSeries( coord , time4D);

        gl_FragColor = mix(gl_FragColor,vec4(vec3(0.0, 0.5, 1.0) + n, 1.0),1.0);

      }
</script>
<script id="vertexShaderNoise" type="x-shader/x-vertex">

  varying vec3 vPosition;
  uniform vec3 scale;
  uniform float time4D;
  uniform float timeTranslation;

  void main( void ) {

    // vUv = uv * scale;
    vPosition = position/scale;

    vec3 coord = vec3(vPosition.x + timeTranslation,vPosition.y,vPosition.z);
    float n = harmonicSeries( coord , time4D);

    gl_Position = projectionMatrix * modelViewMatrix * vec4(
      // position, 1.0);
      position + normal.xyz * clamp(n * 50.0,-50.0,50.0) , 1.0 );

  }

</script>
<head>
</head>
<body>
<script type="text/javascript">

document.documentElement.style.overflow = 'hidden';

// Scene
var scene = new THREE.Scene();

// Camera
var camera = new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 1, 10000);
camera.position.z = 400;
camera.lookAt(new THREE.Vector3(0, 0, 0));
scene.add(camera);

var controls = new THREE.TrackballControls(camera);

// Renderer
var renderer = new THREE.WebGLRenderer({
  antialias: true
});
renderer.setSize(window.innerWidth, window.innerHeight);
renderer.setClearColor(0xffffff);

document.body.appendChild(renderer.domElement);

// Initialize Shaders
nCode = document.getElementById( 'noise' ).textContent;
vShader = document.getElementById( 'vertexShaderNoise' ).textContent;
vShader = nCode.concat(vShader);
fShader = document.getElementById( 'fragmentShaderNoise' ).textContent;
fShader = nCode.concat(fShader);

// Plane Shader
pVShader = document.getElementById( 'vertexShaderPlane').textContent;
pFShader = document.getElementById( 'fragmentShaderPlane').textContent;
pFShader = nCode.concat(pFShader)

uniforms = 
THREE.UniformsUtils.merge([
  THREE.ShaderLib.lambert.uniforms,{
    time4D:  { type: "f", value: 1.0 },
    timeTranslation: { type: "f", value: 1.0 },
    scale: { type: "v3", value: new THREE.Vector3( 64, 64, 64 ) },
    translate: { type: "f", value: true},
    move: { type: "f", value: true},
    }]);

// Create Sphere Material
var sphereMaterial = new THREE.ShaderMaterial({
    uniforms: uniforms,
    vertexShader: vShader,
    fragmentShader: fShader,
    wireframe: false,
    color: 0xff0000,
    lights: true
});


// Sphere
var geometry = new THREE.SphereGeometry( 100, 64, 64 );
var sphere = new THREE.Mesh(geometry,sphereMaterial); 
sphere.position.set(150,0,0);
scene.add( sphere );

// Create Plane Material
var planeMaterial = new THREE.ShaderMaterial({
    uniforms: uniforms,
    vertexShader: pVShader,
    fragmentShader: pFShader,
    wireframe: false,
    color: 0xff0000,
    lights: false
});

// Plane
var width,height;
width = height = 200;
var geometry = new THREE.PlaneGeometry(width, height, 20, 20);
var plane = new THREE.Mesh(geometry,planeMaterial); 
scene.add(plane);
plane.position.set(-150,0,0);
plane.lookAt( camera.position );


window.addEventListener('resize', onWindowResize, false);
function onWindowResize(){
  camera.aspect = window.innerWidth / window.innerHeight;
  camera.updateProjectionMatrix();
  renderer.setSize(window.innerWidth, window.innerHeight);
};

var paused = true;
function update() {
  if(uniforms.move.value){
    uniforms.time4D.value += 0.005;
  }
  if(uniforms.translate.value){
    uniforms.timeTranslation.value += 0.005;
  }
  plane.quaternion.copy( camera.quaternion );
  requestAnimationFrame(update);
  controls.update();
  renderer.render(scene, camera);
}

// Keyboard Listeners
document.addEventListener("keydown", function(e) {
  // up
  if ( e.keyCode === 38 ){
    sphere.rotation.x += Math.PI/48;
  } 
  // down
  else if ( e.keyCode === 40 ){
    sphere.rotation.x -= Math.PI/48;
  }
  // left
  else if ( e.keyCode === 37 ){
    sphere.rotation.y += Math.PI/48;
  }
  // right
  else if ( e.keyCode === 39 ){
    sphere.rotation.y -= Math.PI/48;
  }
  // space
  else if ( e.keyCode === 32 ){
    // paused = !paused;
    uniforms.move.value = !uniforms.move.value;
  }
  // w
  else if ( e.keyCode === 87){
    sphere.material.wireframe = !sphere.material.wireframe;
  }
  // q
  else if ( e.keyCode === 65){
    sphere.position.z += 10;
  }
  // a
  else if ( e.keyCode === 81){
    sphere.position.z -= 10; 
  }
  // t
  else if ( e.keyCode === 84){
    uniforms.translate.value = !uniforms.translate.value;
  }
}, false);

update();

</script>
</body>
