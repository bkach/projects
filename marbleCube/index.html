<!DOCTYPE html>
<html lang="en">
<head>
<style>
</style>
</head>
<script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r71/three.min.js"></script>
<script src="../lib/Trackball.js"></script>
<script id="noise" type="x-shader/x-fragment">
  //
  // Description : Array and textureless GLSL 2D/3D/4D simplex 
  //               noise functions.
  //      Author : Ian McEwan, Ashima Arts.
  //  Maintainer : ijm
  //     Lastmod : 20110822 (ijm)
  //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
  //               Distributed under the MIT License. See LICENSE file.
  //               https://github.com/ashima/webgl-noise
  // 

  vec4 mod289(vec4 x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

  float mod289(float x) {
    return x - floor(x * (1.0 / 289.0)) * 289.0; }

  vec4 permute(vec4 x) {
       return mod289(((x*34.0)+1.0)*x);
  }

  float permute(float x) {
       return mod289(((x*34.0)+1.0)*x);
  }

  vec4 taylorInvSqrt(vec4 r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }

  float taylorInvSqrt(float r)
  {
    return 1.79284291400159 - 0.85373472095314 * r;
  }

  vec4 grad4(float j, vec4 ip)
    {
    const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
    vec4 p,s;

    p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
    p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
    s = vec4(lessThan(p, vec4(0.0)));
    p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www; 

    return p;
    }
              
  // (sqrt(5) - 1)/4 = F4, used once below
  #define F4 0.309016994374947451

  float snoise(vec4 v)
    {
    const vec4  C = vec4( 0.138196601125011,  // (5 - sqrt(5))/20  G4
                          0.276393202250021,  // 2 * G4
                          0.414589803375032,  // 3 * G4
                         -0.447213595499958); // -1 + 4 * G4

    // First corner
      vec4 i  = floor(v + dot(v, vec4(F4)) );
      vec4 x0 = v -   i + dot(i, C.xxxx);

    // Other corners

    // Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
      vec4 i0;
      vec3 isX = step( x0.yzw, x0.xxx );
      vec3 isYZ = step( x0.zww, x0.yyz );
    //  i0.x = dot( isX, vec3( 1.0 ) );
      i0.x = isX.x + isX.y + isX.z;
      i0.yzw = 1.0 - isX;
    //  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
      i0.y += isYZ.x + isYZ.y;
      i0.zw += 1.0 - isYZ.xy;
      i0.z += isYZ.z;
      i0.w += 1.0 - isYZ.z;

      // i0 now contains the unique values 0,1,2,3 in each channel
      vec4 i3 = clamp( i0, 0.0, 1.0 );
      vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
      vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

      //  x0 = x0 - 0.0 + 0.0 * C.xxxx
      //  x1 = x0 - i1  + 1.0 * C.xxxx
      //  x2 = x0 - i2  + 2.0 * C.xxxx
      //  x3 = x0 - i3  + 3.0 * C.xxxx
      //  x4 = x0 - 1.0 + 4.0 * C.xxxx
      vec4 x1 = x0 - i1 + C.xxxx;
      vec4 x2 = x0 - i2 + C.yyyy;
      vec4 x3 = x0 - i3 + C.zzzz;
      vec4 x4 = x0 + C.wwww;

    // Permutations
      i = mod289(i); 
      float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
      vec4 j1 = permute( permute( permute( permute (
                 i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
               + i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
               + i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
               + i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));

    // Gradients: 7x7x6 points over a cube, mapped onto a 4-cross polytope
    // 7*7*6 = 294, which is close to the ring size 17*17 = 289.
      vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

      vec4 p0 = grad4(j0,   ip);
      vec4 p1 = grad4(j1.x, ip);
      vec4 p2 = grad4(j1.y, ip);
      vec4 p3 = grad4(j1.z, ip);
      vec4 p4 = grad4(j1.w, ip);

    // Normalise gradients
      vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
      p0 *= norm.x;
      p1 *= norm.y;
      p2 *= norm.z;
      p3 *= norm.w;
      p4 *= taylorInvSqrt(dot(p4,p4));

    // Mix contributions from the five corners
      vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
      vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
      m0 = m0 * m0;
      m1 = m1 * m1;
      return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
                   + dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

    }

  float turbulence( vec3 coord, float time, int fractals) {

    float n = 0.0;

    if ( fractals >= 1 ){
      n += 1.0    *      abs(snoise(vec4(coord / 1.0 , time)));
    }
    if ( fractals >= 2 ){
      n += (1.0/2.0)  *  abs(snoise(vec4(coord / (1.0/2.0) , time)));
    }
    if ( fractals >= 3 ){
      n += (1.0/4.0)  *  abs(snoise(vec4(coord / (1.0/4.0) , time)));
    }
    if ( fractals >= 4 ){
      n += (1.0/8.0)  *  abs(snoise(vec4(coord / (1.0/8.0) , time)));
    }
    if (fractals >= 5){
      n += (1.0/16.0) *  abs(snoise(vec4(coord / (1.0/16.0) , time)));
    }
    if (fractals >= 6){
      n += (1.0/32.0) *  abs(snoise(vec4(coord / (1.0/32.0) , time)));
    }
    if (fractals >= 7){
      n += (1.0/64.0) *  abs(snoise(vec4(coord / (1.0/64.0) , time)));
    }
    if (fractals >= 8){
      n += (1.0/128.0) * abs(snoise(vec4(coord / (1.0/128.0) , time)));
    }
    if (fractals >= 9){
     n += (1.0/256.0) *  abs(snoise(vec4(coord / (1.0/256.0) , time)));
    }

    return n;
  }

  float harmonicSeries( vec3 coord, float time, int fractals) {

    float n = 0.0;

    if ( fractals >= 1 ){
      n += 1.0    *      snoise(vec4(coord / 1.0 , time));
    }
    if ( fractals >= 2 ){
      n += (1.0/2.0)  *  snoise(vec4(coord / (1.0/2.0) , time));
    }
    if ( fractals >= 3 ){
      n += (1.0/4.0)  *  snoise(vec4(coord / (1.0/4.0) , time));
    }
    if ( fractals >= 4 ){
      n += (1.0/8.0)  *  snoise(vec4(coord / (1.0/8.0) , time));
    }
    if (fractals >= 5){
      n += (1.0/16.0) *  snoise(vec4(coord / (1.0/16.0) , time));
    }
    if (fractals >= 6){
      n += (1.0/32.0) *  snoise(vec4(coord / (1.0/32.0) , time));
    }
    if (fractals >= 7){
      n += (1.0/64.0) *  snoise(vec4(coord / (1.0/64.0) , time));
    }
    if (fractals >= 8){
      n += (1.0/128.0) * snoise(vec4(coord / (1.0/128.0) , time));
    }
    if (fractals >= 9){
     n += (1.0/256.0) *  snoise(vec4(coord / (1.0/256.0) , time));
    }

    return n;
  }

</script>
<script id="composition" type="x-shader/x-fragment">
  
  #define PI 3.1415926535897932384626433832795

  float stripes(float thickness,
                float noiseIntensity,
                float degrees,
                float noise,
                vec3 position,
                float edgeHardness,
                float zoom){
    
    // Stripes / Marble
    float radians = degrees * (PI/180.0);
    float sine = 
    pow(
        cos(
          (position.x * sin(-radians) + 
           position.y * cos(-radians)) / zoom + noise * noiseIntensity)
      ,thickness);
    float mixVal = sine;

    float sine2 = 
    pow(
        cos(
          (position.x * sin(-PI/2.0) + 
           position.y * cos(-PI/2.0)) / zoom + noise * noiseIntensity)
      ,thickness);
    mixVal = (sine + sine2) / 2.0;

    if (mixVal <= edgeHardness){
      mixVal = 0.0;
    }
    // else{
    //   mixVal = 1.0;
    // }

    return mixVal;
  }
</script>
<script id="fragmentShaderNoise" type="x-shader/x-fragment">
      uniform float time;
      varying vec3 vPosition;
      varying vec3 vNormal;

      uniform float thickness;
      uniform float noiseIntensity;
      uniform float degrees;
      uniform float edgeHardness;
      uniform float zoom;
      uniform int fractals;
      uniform float sineOn;
      uniform float doubleSine;

      void main( void ) {

        // Lighting Stuff
        vec3 light = vec3(50.0,100.0,100.0);
        light = normalize(light);
        float dProd = max(0.0, dot(vNormal, light));

        // Define Colors
        vec3 lightColor = vec3(dProd,dProd,dProd);
        vec3 whiteColor   = vec3(255.0/255.0, 255.0/255.0, 255.0/255.0);
        vec3 brownColor  = vec3(66.0/255.0, 37.0/255.0, 24.0/255.0);
        vec3 redColor = vec3(1.0,0.0,0.0);
        vec3 greenColor = vec3(0.0,1.0,0.0);
        vec3 blueColor = vec3(0.0,0.0,1.0);        
        vec3 blackColor = vec3(0.0,0.0,0.4);
        vec3 color1 = brownColor;
        vec3 color2 = whiteColor;

        // Noise
        vec3 coord = vec3(vPosition.x,vPosition.y,vPosition.z);
        vec3 coord1 = vec3(vPosition.x + time/3.0,vPosition.y,vPosition.z);
        vec3 coord2 = vec3(vPosition.x - time/3.0,vPosition.y,vPosition.z);
        float noise = harmonicSeries( coord / zoom, 1.0, fractals);
        float noise1 = harmonicSeries( coord1 / zoom, 1.0, fractals);
        float noise2 = turbulence( coord2 / zoom, 1.0, fractals);

        // Stripes / Marble
        float result = 1.0;
        if (sineOn == 1.0){
          result = stripes(thickness,noiseIntensity,degrees,noise,vPosition,edgeHardness,zoom);
          if(doubleSine == 1.0){
            result += stripes(thickness,noiseIntensity,degrees+90.0,noise,vPosition,edgeHardness,zoom);
            result /= 2.0;
          }
        }

        // float clampValue = 0.4;
        // float halfClamp = clampValue / 2.0;
        // result = clamp(noise,0.0,clampValue);

        //result = (noise1 + noise2) / 2.0;
        
        gl_FragColor = vec4(
          mix(lightColor,
            mix(color1,
              color2,
              result),
            0.5)
          ,1.0);

        // if(result > halfClamp){
        //   gl_FragColor = vec4(blueColor,1.0);
        //   gl_FragColor = vec4(
        //     mix(blueColor,blackColor,
        //       ( (result - halfClamp) / halfClamp )
        //       )
        //     ,1.0);
        // }
        // else{
        //   gl_FragColor = vec4(greenColor,1.0);
        //   gl_FragColor = vec4(
        //     mix(whiteColor,greenColor,
        //       ( result / halfClamp )
        //       )
        //     ,1.0);
        // }

        // vec3 coord2 = vec3(vPosition.x + timeTranslation * 20.0,vPosition.y,vPosition.z);
        // gl_FragColor = mix(gl_FragColor,vec4(whiteColor,1.0), noise2);
      }
</script>
<script id="vertexShaderNoise" type="x-shader/x-vertex">

  varying vec3 vPosition;
  //uniform vec3 scale;
  varying vec3 vNormal;

  void main( void ) {

    vPosition = position;
    vNormal = normal;

    vec3 coord = vec3(vPosition.x,vPosition.y,vPosition.z);
    float displacement = 0.0;
    float noise = 1.0;
    vec3 newPosition = position;

    gl_Position = projectionMatrix * modelViewMatrix * vec4(
       // stripes(position),
       position,
       1.0);
  }
</script>
<head>
</head>
<body>
<script type="text/javascript">

document.documentElement.style.overflow = 'hidden';

// Scene
var scene = new THREE.Scene();

// Camera
var camera = new THREE.PerspectiveCamera(45, window.innerWidth / window.innerHeight, 1, 10000);
camera.position.z = 400;
camera.lookAt(new THREE.Vector3(0, 0, 0));
scene.add(camera);

// Controls
trackball = new THREE.TrackballControls(camera);

// Renderer
var renderer = new THREE.WebGLRenderer({
  antialias: true
});
renderer.setSize(window.innerWidth, window.innerHeight);
renderer.setClearColor(0xffffff);

document.body.appendChild(renderer.domElement);

// Initialize Shaders
nCode = document.getElementById( 'noise' ).textContent;
cCode = document.getElementById( 'composition' ).textContent;
vShader = document.getElementById( 'vertexShaderNoise' ).textContent;
vShader = nCode.concat(cCode).concat(vShader);
fShader = document.getElementById( 'fragmentShaderNoise' ).textContent;
fShader = nCode.concat(cCode).concat(fShader);

uniforms = 
THREE.UniformsUtils.merge([
  THREE.ShaderLib.lambert.uniforms,{
    //scale: { type: "v3", value: new THREE.Vector3( 64, 64, 64 ) },
    //translate: { type: "f", value: false},
    //move: { type: "f", value: false},
    doubleSine: {type: "f", value: false},
    time: {type: "f", value: 1.0},

    thickness: { type: "f", value: 0.25},
    noiseIntensity: { type: "f", value: 1.0},
    degrees: { type: "f", value: 45.0},
    edgeHardness: { type: "f", value: 0.0},
    zoom: { type: "f", value: 30.0 },
    fractals: { type: "i", value: 9 },
    sineOn: { type: "f", value: 1.0}

    //thickness: { type: "f", value: 1.0},
    //noiseIntensity: { type: "f", value: 1.0},
    //degrees: { type: "f", value: 0.0},
    //edgeHardness: { type: "f", value: 0.0},
    //zoom: { type: "f", value: 1.0 },
    //fractals: { type: "i", value: 0 },
    //sineOn: { type: "f", value: 0.0}

    }]);

// Create Sphere Material
var material = new THREE.ShaderMaterial({
    uniforms: uniforms,
    vertexShader: vShader,
    fragmentShader: fShader,
    wireframe: false,
    color: 0xff0000,
    lights: true
});


// Objects
var sphereGeo = new THREE.SphereGeometry( 100, 64, 64 );
var geometries = [
  new THREE.SphereGeometry( 100, 64, 64 ),
  new THREE.BoxGeometry( 100, 64, 64 ),
  new THREE.CylinderGeometry( 50, 50, 100, 100),
  new THREE.TorusGeometry( 100, 10, 64, 64 ),
  new THREE.TorusKnotGeometry( 100, 30, 64, 8 )
];
var currentObjectCount = 0;
var currentObject = new THREE.Mesh(geometries[currentObjectCount],material); 
scene.add( currentObject );

window.addEventListener('resize', onWindowResize, false);
function onWindowResize(){
  camera.aspect = window.innerWidth / window.innerHeight;
  camera.updateProjectionMatrix();
  renderer.setSize(window.innerWidth, window.innerHeight);
};

var paused = true;
function update() {

//    thickness: { type: "f", value: 1.0},
//    noiseIntensity: { type: "f", value: 0.0},
//    degrees: { type: "f", value: 0.0},
//    edgeHardness: { type: "f", value: 0.0},
//    zoom: { type: "f", value: 1.0 }

  uniforms.time.value += 1.0;

  requestAnimationFrame(update);
  renderer.render(scene, camera);
  trackball.update();
}

// Keyboard Listeners
document.addEventListener("keydown", function(e) {
  // space
  if ( e.keyCode === 32 ){
    scene.remove( currentObject );
    currentObjectCount += 1;
    currentObject = new THREE.Mesh(geometries[currentObjectCount % geometries.length],material);
    scene.add( currentObject );
  }
  // up
  else if ( e.keyCode === 38 ){
    uniforms.zoom.value += 0.5;
  }
  // down
  else if ( e.keyCode === 40 ){
    uniforms.zoom.value -= 0.5;
  }
  // left
  else if ( e.keyCode === 37 ){
    uniforms.degrees.value += 1;
  }
  // right
  else if ( e.keyCode === 39 ){
    uniforms.degrees.value -= 1;
  }
  // T 
  else if ( e.keyCode === 84 ){
    uniforms.thickness.value += 0.1;
  }
  // G
  else if ( e.keyCode === 71 ){
    uniforms.thickness.value -= 0.1;
  }
  // R 
  else if ( e.keyCode === 82 ){
    uniforms.fractals.value += 1;
  }
  // F
  else if ( e.keyCode === 70 ){
    uniforms.fractals.value -= 1;
  }
  // S
  else if ( e.keyCode === 83 ){
    uniforms.sineOn.value = !uniforms.sineOn.value;
  }
  // w
  else if ( e.keyCode === 68 ){
    uniforms.doubleSine.value = !uniforms.doubleSine.value;
  }
}, false);

update();

</script>
</body>
